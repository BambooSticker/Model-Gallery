{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Gurobi, GLMakie, Polyhedra, CDDLib"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Paper Reproduction: Two-Stage Robust Optimization\n",
    "\n",
    "This notebook presents a Julia implementation of the column-and-constraint generation algorithm for solving two-stage robust optimization problems. The case study is a robust location-transportation problem, and the model settings can be found in the following original paper: \n",
    "\n",
    "**Reference**: *Zeng, Bo, and Long Zhao. \"Solving two-stage robust optimization problems using a column-and-constraint generation method.\" Operations Research Letters 41.5 (2013): 457-461.*\n",
    "\n",
    "---"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Sets and Indices\n",
    "- $\\mathcal{I} = \\{1,\\dots,m\\} $: facility sites (potential plants/warehouses)  \n",
    "- $\\mathcal{J} = \\{1,\\dots,n\\} $: customers  \n",
    "- Indices: $i \\in \\mathcal{I}$, $j \\in \\mathcal{J}$\n",
    "\n",
    "#### Data (Parameters)\n",
    "- $f_i \\ge 0$: fixed opening cost at site $i$  \n",
    "- $a_i \\ge 0$: unit capacity installation cost at site $i$  \n",
    "- $K_i \\ge 0$: maximum installable capacity at site $i$  \n",
    "- $c_{ij} \\ge 0$: per-unit transportation cost from site $i$ to customer $j$\n",
    "- $\\bar d_j \\ge 0$: nominal (baseline) demand of customer $j$  \n",
    "- $\\tilde d_j \\ge 0$: maximum positive deviation of demand $j$ \n",
    "- $\\Gamma \\in \\mathbb{Z}_+$: uncertainty budget controlling conservatism"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1-element Vector{Vector{Float64}}:\n",
       " [206.0, 274.0, 220.0]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "m = 3\n",
    "n = 3\n",
    "f = [400, 414, 326]\n",
    "a = [18, 25, 20]\n",
    "K = 800\n",
    "c = [22 33 24;\n",
    "     33 23 30;\n",
    "     20 25 27]\n",
    "d_nom = [206.0, 274.0, 220.0]\n",
    "d_dev = [40.0, 40.0, 40.0]\n",
    "Γ = [1.8, 1.2]\n",
    "\n",
    "data = Dict(:m=>m, :n=>n, :f=>f, :a=>a, :K=>K,\n",
    "            :c=>c, :d_nom=>d_nom, :d_dev=>d_dev, :Γ=>Γ)\n",
    "\n",
    "d_scenarios = Vector{Vector{Float64}}()\n",
    "push!(d_scenarios, d_nom)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "\n",
    "#### Decision Variables (Complete List)\n",
    "**First stage**\n",
    "- $y_i \\in \\{0,1\\}$ — open facility $i$ (1) or not (0)  \n",
    "- $z_i \\ge 0$ — installed capacity at facility $i$\n",
    "\n",
    "**Second stage (recourse, depend on realized $d$)**\n",
    "- $x_{ij} \\ge 0$ — shipped quantity from facility $i$ to customer $j$\n",
    "\n",
    "**Auxiliary (epigraph)**\n",
    "- $\\eta \\in \\mathbb{R}$ — upper bound on worst-case second-stage transportation cost\n",
    "\n",
    "---"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Two-Stage Robust Optimization (Min–Max–Min)\n",
    "We consider a location–transportation system with uncertain customer demand captured by a budgeted polyhedral uncertainty set. First-stage decisions open facilities and install capacities; second-stage (recourse) decisions route flow to meet the realized demand.\n",
    "\n",
    "$$\n",
    "\\begin{aligned}\n",
    "\\min_{(y,z)\\in \\mathcal{S}_y}\\quad\n",
    "& \\sum_{i\\in\\mathcal{I}} f_i y_i \\;+\\; \\sum_{i\\in\\mathcal{I}} a_i z_i\n",
    "\\;+\\;\n",
    "\\underbrace{\\max_{d \\in \\mathcal{D}}\n",
    "\\ \\min_{x \\in \\mathcal{S}_x(d;y,z)}\n",
    "\\ \\sum_{i\\in\\mathcal{I}}\\sum_{j\\in\\mathcal{J}} c_{ij} x_{ij}}_{\\text{worst-case recourse cost}}.\n",
    "\\end{aligned}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Uncertainty Set (Budgeted Demand)\n",
    "\n",
    "$$\n",
    "\\mathcal{D}\n",
    "=\n",
    "\\left\\{\n",
    "d \\in \\mathbb{R}_+^{n} :\n",
    "\\ d_j \\;=\\; \\bar d_j + g_j \\tilde d_j,\\;\\; 0 \\le g_j \\le 1\\ \\forall j,\\;\\;\n",
    "\\sum_{j\\in\\mathcal{J}} g_j \\le \\Gamma\n",
    "\\right\\}.\n",
    "$$\n",
    "\n",
    "#### First-Stage Feasible Set\n",
    "$$\n",
    "\\mathcal{S}_y \\;=\\; \\left\\{(y,z) \\in \\{0,1\\}^{m} \\times \\mathbb{R}_+^{m} \\;:\\; z_i \\le K_i\\, y_i \\ \\forall i \\in \\mathcal{I} \\right\\}.\n",
    "$$\n",
    "\n",
    "#### Second-Stage Feasible Set (for a realized $d \\in \\mathcal{D}$)\n",
    "$$\n",
    "\\mathcal{S}_x(d;y,z) \\;=\\; \\left\\{\n",
    "x \\in \\mathbb{R}_+^{m \\times n} \\;:\\;\n",
    "\\sum_{j\\in\\mathcal{J}} x_{ij} \\le z_i \\ \\forall i,\\quad\n",
    "\\sum_{i\\in\\mathcal{I}} x_{ij} \\ge d_j \\ \\forall j\n",
    "\\right\\}.\n",
    "$$\n",
    "\n",
    "We assume **relatively complete recourse** (e.g., $\\sum_{i} K_i \\ge \\max_{d\\in \\mathcal{D}} \\sum_j d_j$) so that feasible second-stage solutions exist for any feasible first-stage decision. The following auxiliary model helps calculate the maximal total demand that could ever happen given the uncertainty set $\\mathcal{D}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "get_maximal_demand (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function get_maximal_demand(data)\n",
    "    I = 1:data[:m] \n",
    "    J = 1:data[:n]\n",
    "    d_nom, d_dev, Γ = data[:d_nom], data[:d_dev], data[:Γ]\n",
    "    model = Model(Gurobi.Optimizer)\n",
    "    set_silent(model)\n",
    "    @variables(model, begin\n",
    "        d[j in J] >= 0\n",
    "        0 <= g[j in J] <= 1\n",
    "    end)\n",
    "\n",
    "    @constraints(model, begin\n",
    "        [j in J], d[j] == d_nom[j] + g[j] * d_dev[j]\n",
    "        g[1] + g[2] + g[3] <= Γ[1]\n",
    "        g[1] + g[2] <= Γ[2]\n",
    "    end)\n",
    "    \n",
    "    @objective(model, Max, sum(d[j] for j in J))\n",
    "    optimize!(model)\n",
    "    return objective_value(model)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Master Problem\n",
    "\n",
    "To derive the master problem, introducing an epigraph variable $\\eta$ for the worst-case second-stage cost:\n",
    "$$\n",
    "\\begin{aligned}\n",
    "\\min_{(y,z)\\in \\mathcal{S}_y,\\ \\eta}\\quad\n",
    "& \\sum_{i} f_i y_i + \\sum_{i} a_i z_i + \\eta \\\\\n",
    "\\text{s.t.}\\quad\n",
    "& \\eta \\;\\ge\\; \\min_{x \\in \\mathcal{S}_x(d;y,z)} \\sum_{i,j} c_{ij} x_{ij}\n",
    "\\quad \\forall d \\in \\mathcal{D}.\n",
    "\\end{aligned}\n",
    "$$\n",
    "\n",
    "For more details about the master and subproblem formulations, please refer to the original paper and its supplementary materials. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "build_master (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function build_master(d_scenarios, data)\n",
    "    \"\"\"\n",
    "    build_master(g_scenarios, data) -> Model, y, z, η\n",
    "    - d_scenarios: Vector of vectors, each of length n\n",
    "        [matrix]: L by J \n",
    "    - data: Dict containing m, n, f, a, K, c\n",
    "    Returns: JuMP.Model with variables y,z,η and recourse x[l]\n",
    "    \"\"\"\n",
    "    I = 1:data[:m] \n",
    "    J = 1:data[:n]\n",
    "    f, a, K, c = data[:f], data[:a], data[:K], data[:c]\n",
    "    # d_nom, d_dev, Γ = data[:d_nom], data[:d_dev], data[:Γ]\n",
    "    d = d_scenarios\n",
    "\n",
    "    model = Model(Gurobi.Optimizer)\n",
    "    set_silent(model)\n",
    "\n",
    "    # First‐stage\n",
    "    @variables(model, begin\n",
    "        y[i in I], Bin\n",
    "        z[i in I] >= 0\n",
    "        η\n",
    "    end)\n",
    "\n",
    "    # Recourse vars for each scenario l\n",
    "    L = 1:length(d_scenarios)\n",
    "    @variables(model, begin\n",
    "        x[l in L, i in I, j in J] >= 0\n",
    "    end)\n",
    "\n",
    "    @constraints(model, begin\n",
    "        [i in I], z[i] <= K * y[i]\n",
    "        [l in L], η >= sum(c[i,j]*x[l,i,j] for i in I, j in J)\n",
    "        [l in L, j in J], sum(x[l,i,j] for i in I) >= d[l][j]\n",
    "        [l in L, i in I], sum(x[l,i,j] for j in J) <= z[i]\n",
    "    end)\n",
    "    \n",
    "    # Total capacity is greater than the maximal total demand. \n",
    "    z_min = get_maximal_demand(data)\n",
    "    @constraint(model, sum(z[i] for i in I) >= z_min)\n",
    "\n",
    "    # Objective (MP)\n",
    "    @objective(model, Min,\n",
    "        sum(f[i]*y[i] for i in I)\n",
    "      + sum(a[i]*z[i] for i in I)\n",
    "      + η\n",
    "    )\n",
    "\n",
    "    optimize!(model)\n",
    "    return model, y, z, η\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Subproblem"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "build_subproblem_milp (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function build_subproblem_milp(z_val, data)\n",
    "    \"\"\"\n",
    "    build_subproblem(z_val, data) -> Model, g, xsp\n",
    "    - z_val: vector of length m, the fixed capacity decisions\n",
    "    - data: Dict containing m, n, c, d_nom, d_dev, Γ\n",
    "        m: indices i=1, 2, 3\n",
    "        n: indices j=1, 2, 3\n",
    "        c: [matrix]\n",
    "        d_nom: [R^3]\n",
    "        d_dev: [R^3]\n",
    "        Γ: [R^2] RHS of two constraints in D\n",
    "\n",
    "    Returns: JuMP.Model with variables g[1:n], xsp[1:m,1:n]\n",
    "    \"\"\"\n",
    "    I = 1:data[:m] \n",
    "    J = 1:data[:n]\n",
    "    c, d_nom, d_dev, Γ = data[:c], data[:d_nom], data[:d_dev], data[:Γ]\n",
    "\n",
    "    model = Model(Gurobi.Optimizer)\n",
    "    set_silent(model)\n",
    "\n",
    "    @variables(model, begin\n",
    "        d[j in J] >= 0\n",
    "        π[i in I] >= 0\n",
    "        λ[j in J] >= 0\n",
    "        x[i in I, j in J] >= 0\n",
    "        0 <= g[j in J] <= 1\n",
    "        α[i in I, j in J], Bin\n",
    "        β[j in J], Bin\n",
    "        γ[i in I], Bin\n",
    "    end)\n",
    "\n",
    "    # Big-M relaxation\n",
    "    M = 1000\n",
    "    @constraints(model, begin\n",
    "        [i in I], sum(x[i,j] for j in J) <= z_val[i]\n",
    "        [j in J], sum(x[i,j] for i in I) <= d[j]\n",
    "        [i in I, j in J], λ[j] - π[i] <= c[i,j]\n",
    "        [i in I, j in J], x[i,j] <= M * α[i,j]\n",
    "        [i in I, j in J], (c[i,j] - λ[j] + π[i]) <= (c[i,j]*M)*(1-α[i,j])\n",
    "        [j in J], λ[j] <= M * β[j]\n",
    "        [j in J], (sum(x[i,j] for i in I) - d[j]) <= d_dev[j] * (1-β[j])\n",
    "        [i in I], π[i] <= M * γ[i]\n",
    "        [i in I], (z_val[i] - sum(x[i,j] for j in J)) <= z_val[i] * (1-γ[i])\n",
    "        [j in J], d[j] == d_nom[j] + g[j] * d_dev[j]\n",
    "        g[1] + g[2] + g[3] <= Γ[1]\n",
    "        g[1] + g[2] <= Γ[2]\n",
    "    end)\n",
    "\n",
    "    @objective(model, Max,\n",
    "        sum(c[i,j] * x[i,j] for i in I, j in J)\n",
    "    )\n",
    "\n",
    "    optimize!(model)\n",
    "\n",
    "    return model, d, λ, π, x\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Implementation of the C\\&CG Algorithm"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "col_constr_gen (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function col_constr_gen()\n",
    "    mp_list = JuMP.Model[]      # for masters\n",
    "    sp_list = JuMP.Model[]      # for subproblems\n",
    "\n",
    "    LB, UB, k = -Inf, Inf, 0\n",
    "    convergence = false\n",
    "    while !convergence\n",
    "        # 1. build & solve master\n",
    "        mp, y, z, η = build_master(d_scenarios, data)\n",
    "        push!(mp_list, mp)       # save it\n",
    "\n",
    "        LB = objective_value(mp)\n",
    "        println(\"Lower bound: $LB\")\n",
    "        y_val, z_val = value.(y), value.(z)\n",
    "\n",
    "        # 2. build & solve subproblem\n",
    "        sp, d, λ, π, x = build_subproblem_milp(z_val, data)\n",
    "        push!(sp_list, sp) \n",
    "\n",
    "        Q = objective_value(sp)\n",
    "        UB = min(UB, sum(f[i]*y_val[i] + a[i]*z_val[i] for i in 1:m) + Q)\n",
    "        println(\"Upper bound: $UB\")\n",
    "        \n",
    "        # 3. record new scenario\n",
    "        push!(d_scenarios, value.(d))\n",
    "        k += 1\n",
    "        if (UB - LB) <= 1e-4\n",
    "            convergence = true\n",
    "            return mp, sp\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Solution and the visualization of uncertainty set\n",
    "\n",
    "$$\n",
    "\\mathcal{D} =\n",
    "\\Big\\{\n",
    "d :\n",
    "d_0 = 206 + 40 g_0,\\;\n",
    "d_1 = 274 + 40 g_1,\\;\n",
    "d_2 = 220 + 40 g_2, \\\\ \n",
    "0 \\le g_0 \\le 1,\\;\n",
    "0 \\le g_1 \\le 1,\\;\n",
    "0 \\le g_2 \\le 1, \\\\\n",
    "g_0 + g_1 + g_2 \\le 1.8,\\;\n",
    "g_0 + g_1 \\le 1.2\n",
    "\\Big\\}.\n",
    "$$\n",
    "\n",
    "Since we only have three uncertain demands, the uncertainty set as a polyhedron can be visualized in 3-D space leveraging Polyhedra.jl. Notice that we visualize $d_i$ given the relations of $g_i$, so the halfspaces need some derivations. For example, the first halfspace $d_0+d_1+d_2 \\le 772$ comes from $g_0+g_1+g_2 \\le 1.8$:\n",
    "\n",
    "\\begin{aligned}\n",
    "    d_0+d_1+d_2 & = (206 + 40 g_0) + (274 + 40 g_1) + (220 + 40 g_2) \\\\\n",
    "    & = 700 + 40(g_0+g_1+g_2) \\\\\n",
    "    & \\le 700 + 40*1.8 \\\\\n",
    "    & = 772\n",
    "\\end{aligned}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Set parameter Username\n",
      "Set parameter LicenseID to value 2679099\n",
      "Academic license - for non-commercial use only - expires 2026-06-18\n",
      "Set parameter Username\n",
      "Set parameter LicenseID to value 2679099\n",
      "Academic license - for non-commercial use only - expires 2026-06-18\n",
      "Lower bound: 31832.0\n",
      "Set parameter Username\n",
      "Set parameter LicenseID to value 2679099\n",
      "Academic license - for non-commercial use only - expires 2026-06-18\n",
      "Upper bound: 33680.0\n",
      "Set parameter Username\n",
      "Set parameter LicenseID to value 2679099\n",
      "Academic license - for non-commercial use only - expires 2026-06-18\n",
      "Set parameter Username\n",
      "Set parameter LicenseID to value 2679099\n",
      "Academic license - for non-commercial use only - expires 2026-06-18\n",
      "Lower bound: 33680.0\n",
      "Set parameter Username\n",
      "Set parameter LicenseID to value 2679099\n",
      "Academic license - for non-commercial use only - expires 2026-06-18\n",
      "Upper bound: 33680.0\n"
     ]
    },
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "┌ Warning: Found `resolution` in the theme when creating a `Scene`. The `resolution` keyword for `Scene`s and `Figure`s has been deprecated. Use `Figure(; size = ...` or `Scene(; size = ...)` instead, which better reflects that this is a unitless size and not a pixel resolution. The key could also come from `set_theme!` calls or related theming functions.\n",
      "└ @ Makie /Users/zheli/.julia/packages/Makie/aJUtI/src/scenes.jl:259\n"
     ]
    }
   ],
   "source": [
    "mp, sp = col_constr_gen()\n",
    "\n",
    "planes = [\n",
    "    HalfSpace([ 1.0,  1.0,  1.0], 772.0),   # g0 + g1 + g2 ≤ 1.8\n",
    "    HalfSpace([ 1.0,  1.0,  0.0], 528.0),   # g0 + g1 ≤ 1.2\n",
    "    HalfSpace([ 1.0,  0.0,  0.0], 246.0),   # g0 ≤ 1\n",
    "    HalfSpace([-1.0,  0.0,  0.0], -206.0),  # -g0 ≤ 0\n",
    "    HalfSpace([ 0.0,  1.0,  0.0], 314.0),   # g1 ≤ 1\n",
    "    HalfSpace([ 0.0, -1.0,  0.0], -274.0),  # -g1 ≤ 0\n",
    "    HalfSpace([ 0.0,  0.0,  1.0], 260.0),   # g2 ≤ 1\n",
    "    HalfSpace([ 0.0,  0.0, -1.0], -220.0),  # -g2 ≤ 0\n",
    "]\n",
    "\n",
    "hrepr = reduce(∩, planes)\n",
    "p = polyhedron(reduce(∩, planes), CDDLib.Library())\n",
    "m = Polyhedra.Mesh(p)\n",
    "\n",
    "fig = Figure(resolution = (600, 600))\n",
    "ax  = Axis3(fig[1, 1]; xlabel=\"ξ1\", ylabel=\"ξ2\", zlabel=\"ξ3\", perspectiveness = 0.75)\n",
    "\n",
    "# Filled polyhedron\n",
    "mesh!(ax, m; transparency = true)\n",
    "wireframe!(ax, m; linewidth = 1)\n",
    "\n",
    "# Now plot the two points in red:\n",
    "xs = [206.0, 206.0]\n",
    "ys = [274.0, 314.0]\n",
    "zs = [220.0, 252.0]\n",
    "\n",
    "scatter!(ax, xs, ys, zs;\n",
    "    markersize = 15,\n",
    "    color      = :red,\n",
    "    label      = \"sample points\"\n",
    ")\n",
    "\n",
    "for (x,y,z) in zip(xs, ys, zs)\n",
    "    text!(ax, string(\"(\",x,\",\",y,\",\",z,\")\"), position = (x,y,z),\n",
    "          align = (:left, :bottom), fontsize = 12)\n",
    "end\n",
    "# save(\"./plots/transportation_uncer_3d.png\", fig, px_per_unit = 8.0)\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Take-away:**\n",
    "1. The C\\&CG algorithm is extremely efficient for two-stage RO problems. In this case study, it converges in only 2 iterations. \n",
    "2. C\\&CG expolits a key property of polyhedral uncertainty sets: the worst-case always lies at one of the extreme points. The algorithm iteratively solves MP to obtains a candidate first-stage decision, throws it into SP to identify a worst-case extreme point, and adds this realization back to MP. This procedure continues until the optimality gap reaches zero, i.e., the worst realization is found. \n",
    "> One beautiful thing about solving linear problems is that we can turn a infinite continous search into a finite searching over extreme points, which we have already found in simplex algorithm. "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
